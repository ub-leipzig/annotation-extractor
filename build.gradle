buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.junit.platform:junit-platform-gradle-plugin:1.0.1")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.15.0'
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'org.sonarqube' version '2.5'
}

ext {
    moduleName = "de.ubleipzig.extractor"
    vendor = 'UB Leipzig'
    license = 'Apache 2'

    trellisApiVersion = '0.3.0'
    trellisVocabularyVersion = '0.1.0'
    trellisJenaIoVersion = '0.2.0'
    jacksonVersion = '2.9.1'
    commonsRdfVersion = '0.3.0-incubating'
    slf4jVersion = '1.7.25'
    jenaVersion = '3.4.0'
    mustacheVersion = '0.9.5'
    jerseyVersion = '2.25.1'
    junitVersion = '4.12'
    mockitoVersion = '2.9.0'
    logbackVersion = '1.2.3'
    fcrepoClientVersion = "0.3.0"
    junitVersion = '4.12'
    mockitoVersion = '2.10.0'
    xercesVersion = '2.11.0'
    jacocoVersion = "0.7.9"
}

configurations {
    buildToolsConfig
}

dependencies {
    buildToolsConfig "cool.pandora:pandora-build-tools:0.1.3"
}

task processBuildTools(type: Copy) {
    from {
        configurations.buildToolsConfig.collect {
            zipTree(it).matching {
                include 'checkstyle/*.xml'
                include 'license/*.txt'
            }
        }
    }
    into 'build'
}

configure(allprojects) { project ->
    group 'de.ubleipzig'
    version '1.0-SNAPSHOT'

    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'java-library'

    sourceCompatibility = 1.9
    targetCompatibility = 1.9

    repositories {
        mavenCentral()
        flatDir {
            dirs "$rootProject.projectDir/lib"
        }
    }

    check.dependsOn processBuildTools
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'jacoco'

    afterEvaluate {
        repositories {
            jcenter()
        }

        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.compilerArgs.add("-Xlint:unchecked")
                options.compilerArgs.add("-Xlint:deprecation")
                options.compilerArgs.addAll(["--add-modules", "jdk.incubator.httpclient"])
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                        '--add-modules', 'jdk.incubator.httpclient',
                        '-Xlint:deprecation'
                ]
            }
        }
    }

    checkstyle {
        configFile = rootProject.file('build/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('build/checkstyle/')
        toolVersion = "8.1"
    }

    jacoco {
        toolVersion = "0.7.9"
    }

    license {
        include "**/*.java"
        header rootProject.file('build/license/HEADER.txt')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }
}

project("compliance") {
    dependencies {
        api project(":extractor")
        testImplementation project(":extractor")
        api project(":vocabulary")
        testImplementation project(":vocabulary")
        api group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion
        api group: 'org.trellisldp', name: 'trellis-api', version: trellisApiVersion
        api group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
        testImplementation group: 'junit', name: 'junit', version: junitVersion
        testImplementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
        testImplementation group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testImplementation group: 'xerces', name: 'xercesImpl', version: xercesVersion
        testRuntimeClasspath group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    }
}

project("extractor") {
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.7.9"
    }

    configurations {
        testAgent {
            transitive = false
        }
    }

    dependencies {
        api group: 'org.trellisldp', name: 'trellis-vocabulary', version: trellisVocabularyVersion
        api group: 'org.trellisldp', name: 'trellis-io-jena', version: trellisJenaIoVersion
        api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
        api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
        api group: 'com.github.jsonld-java', name: 'jsonld-java', version: '0.11.1'
        api group: 'org.apache.commons', name: 'commons-text', version: '1.1'
        api group: 'org.xerial', name: 'sqlite-jdbc', version: '3.20.0'
        api group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        api group: 'org.apache.jena', name: 'jena-arq', version: '3.4.0'
        api group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
        api group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion

        implementation group: 'com.github.spullara.mustache.java', name: 'compiler', version: mustacheVersion
        implementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
        implementation group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
        testImplementation("org.junit.vintage:junit-vintage-engine:4.12.0-M5")
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.0.0-M5")
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.0.0-M5")
        testImplementation("org.junit.platform:junit-platform-runner:1.0.0-M5")
        testImplementation group: 'junit', name: 'junit', version: '4.12'
        testImplementation group: 'org.trellisldp', name: 'trellis-io-jena', version: trellisJenaIoVersion
        testImplementation group: 'org.glassfish.jersey.test-framework.providers', name:
                'jersey-test-framework-provider-inmemory', version: jerseyVersion
        testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
        testImplementation group: 'javax.activation', name: 'activation', version: '1.1.1'
        testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    }

    configurations {
        testAgent {
            transitive = false
        }
    }

    tasks.withType(JavaExec) {
        if (it.name == 'junitPlatformTest') {
            doFirst {
                //jvmArgs "-javaagent:${configurations.testAgent.singleFile}=destfile=${project.buildDir
                // .name}/jacoco/test.exec"
                jvmArgs "--add-modules=jdk.incubator.httpclient"
            }
        }
    }

    configurations {
        junitXmlToHtml
    }

    task report {
        doLast {
            def reportsDir = new File(rootProject.buildDir, 'test-reports')
            def summaryDir = new File(rootDir, 'summary-reports')
            def date = new Date();
            def formattedDate = date.format('ddMMyy_HHmm')
            def summaryFileName = "test-summary_" + formattedDate + ".html"
            reportsDir.mkdirs()

            ant.taskdef(
                    name: 'junitReport',
                    classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
                    classpath: configurations.junitXmlToHtml.asPath
            )

            ant.junitReport(todir: "$buildDir/test-results/junit-platform", tofile: "aggregated-test-results.xml") {
                fileset(dir: "$buildDir/test-results/junit-platform")
                report(format: 'frames', todir: reportsDir)
            }

            ant.xslt(in: "${buildDir}/test-results/junit-platform/aggregated-test-results.xml",
                    style: "${rootDir}/report-aggregator.xsl",
                    out: "${summaryDir}/${summaryFileName}",
                    classpath: configurations.antdeps.asPath)
        }
    }

    configurations {
        antdeps
    }

    dependencies {
        antdeps group: 'net.sf.saxon', name: 'saxon', version: '9.1.0.2'
    }

    afterEvaluate {
        def junitPlatformTestTask = tasks.getByName('junitPlatformTest')
        report.dependsOn(junitPlatformTestTask)
        check.dependsOn(report)
    }

    dependencies {
        junitXmlToHtml 'org.apache.ant:ant-junit:1.9.7'
    }
}


project("vocabulary") {
    dependencies {
        api group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion
        api group: 'org.trellisldp', name: 'trellis-api', version: trellisApiVersion
        api group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
        testImplementation group: 'junit', name: 'junit', version: junitVersion
        testImplementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
        testImplementation group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testImplementation group: 'xerces', name: 'xercesImpl', version: xercesVersion

        testRuntimeClasspath group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    }
}



